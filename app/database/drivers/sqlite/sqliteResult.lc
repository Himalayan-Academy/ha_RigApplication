<?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
	put "No direct script access allowed."
	exit to top
end if

##
# revIgniter
#
# An application development framework for LiveCode
# modeled on CodeIgniter
#
# @package		revIgniter
# @author		  rabit@revigniter.com
# @copyright	Copyright (c) 2009 - 2016, dimensionB Bitter u. Bitter GmbH
# @license		http://revigniter.com/userGuide/license.html
# @link		    http://revigniter.com
##

## ------------------------------------------------------------------------

##
# SQLite Result Library
#
# This library extends DBresult.
#
# @category	Database
# @author		rabit@revigniter.com
# @link		  http://revigniter.com/userGuide/database/
##



# DECLARE VARIABLES
local sTableInfo


/*----------------------------------------------------------------------
--| FUNCTION rigNumRows
--|
--| Author: rabit
--| Version:  1.1
--| Created: 11-04-14
--| Last Mod: 18-04-14
--| Requires: rigFetchDBdriverSetting(), rigLogMessage
--|
--| Summary: Number of rows in the result set.
--| 
--| Format:  rigNumRows(param1)
--|
--| Parameters: integer <pResultID>
--|
--| Return: integer
----------------------------------------------------------------------*/

function rigNumRows pResultID
	if pResultID is empty then
		put rigFetchDBdriverSetting("resultid") into tResultID
	else
		put pResultID into tResultID
	end if

	put revNumberOfRecords(tResultID) into tNumRows

	if item 1 of tNumRows is "revdberr" then
		delete item 1 of tNumRows
		rigLogMessage "error", tNumRows
	end if

	return tNumRows
end rigNumRows







/*----------------------------------------------------------------------
--| FUNCTION rigNumFields
--|
--| Author: rabit
--| Version:  1.1
--| Created: 11-04-14
--| Last Mod: 18-04-14
--| Requires: rigFetchDBdriverSetting(), rigLogMessage
--|
--| Summary: Number of fields in the result set.
--| 
--| Format:  rigNumFields(param1)
--|
--| Parameters: integer <pResultID>
--|
--| Return: integer
----------------------------------------------------------------------*/

function rigNumFields pResultID
	if pResultID is empty then
		put rigFetchDBdriverSetting("resultid") into tResultID
	else
		put pResultID into tResultID
	end if

	put revDatabaseColumnCount(tResultID) into tNumFields

	if item 1 of tNumFields is "revdberr" then
		delete item 1 of tNumFields
		rigLogMessage "error", tNumFields
	end if

	return tNumFields
end rigNumFields






/*----------------------------------------------------------------------
--| FUNCTION rigListFields
--|
--| Author: rabit
--| Version:  1.0
--| Created: 11-04-14
--| Last Mod: --
--| Requires: rigFetchDBdriverSetting()
--|
--| Summary: Generates an array of column names.
--| 
--| Format:  rigListFields(param1)
--|
--| Parameters: integer <pResultID>
--|
--| Return: array
----------------------------------------------------------------------*/

function rigListFields pResultID
	if pResultID is empty then
		put rigFetchDBdriverSetting("resultid") into tResultID
	else
		put pResultID into tResultID
	end if

	put revDatabaseColumnNames(tResultID) into tFieldNames
	split tFieldNames using comma

	return tFieldNames
end rigListFields








/*----------------------------------------------------------------------
--| FUNCTION rigFieldData
--|
--| Author: rabit
--| Version:  1.1
--| Created: 11-04-14
--| Last Mod: 14-04-14
--| Requires: rigFetchDBdriverSetting(), _rigPragmaTableInfo, rigDbColumnTypes(),
--| 					rigDbPrimaryField(), rigDbDefaults()
--|
--| Summary: Generates an array of arrays containing field meta-data 
--|					(name, type, maxlength, default, primary).
--| 
--| Format:  rigFieldData(param1)
--|
--| Parameters: string <pTable>
--|
--| Return: array
----------------------------------------------------------------------*/

function rigFieldData pTable
  put TRUE into tTableInfoQuerySuccess
  put empty into tTypes
  put empty into tPrimaryField
  put empty into tDefaults
  
	put rigFetchDBdriverSetting("resultid") into tResultID
	put revDatabaseColumnNames(tResultID) into tFieldNames
	
  # STORE TABLE META DATA IN A VARIABLE
  _rigPragmaTableInfo pTable
  if item 1 of sTableInfo is "revdberr" then
    rigLogMessage "error", "An error occured while trying to query table info data for table:" && pTable
    put FALSE into tTableInfoQuerySuccess
  end if
  
  # GET DATA TYPES ONLY IF TABLE INFO QUERY WAS SUCCESSFULL
  if tTableInfoQuerySuccess is TRUE then
    put rigDbColumnTypes(pTable) into tTypes
  end if
  
	put revDatabaseColumnLengths(tResultID) into tMaxLengths
  
	# GET PRIMARY FIELD AND DEFAULT VALUE ONLY IF
  # TABLE INFO QUERY WAS SUCCESSFULL
	if pTable is not empty then
    if tTableInfoQuerySuccess is TRUE then
		  put rigDbPrimaryField() into tPrimaryField
    
      put rigDbDefaults() into tDefaults
    end if
	end if

	put 0 into tCounter
	repeat for each item thisItem in tFieldNames
		add 1 to tCounter

		put thisItem into tFieldData[tCounter]["name"]
		put item tCounter of tTypes into tFieldData[tCounter]["type"]
		put item tCounter of tMaxLengths into tFieldData[tCounter]["maxlength"]
		put item tCounter of tDefaults into tFieldData[tCounter]["default"]
		if thisItem is tPrimaryField then
			put 1 into tFieldData[tCounter]["primarykey"]
		else
			put "" into tFieldData[tCounter]["primarykey"]
		end if
	end repeat

	return tFieldData
end rigFieldData




/*----------------------------------------------------------------------
--| COMMAND _rigPragmaTableInfo
--|
--| Author: rabit
--| Version:  1.2
--| Created: 12-04-2014
--| Last Mod: 15-04-14
--| Requires: rigDbGetPragma()
--|
--| Summary: Store table meta data in a variable.
--| 
--| Format:  _rigPragmaTableInfo param1
--|
--| Parameters: string <pTable>
--|
--| Return: string
----------------------------------------------------------------------*/

command _rigPragmaTableInfo pTable
  # GET TABLE META DATA
  put "table_info(" &  pTable & ")" into tPragma

  put rigDbGetPragma(tPragma) into sTableInfo
end _rigPragmaTableInfo





/*----------------------------------------------------------------------
--| FUNCTION rigDbColumnTypes
--|
--| Author: rabit
--| Version:  1.0
--| Created: 11-04-14
--| Last Mod: --
--| Requires: --
--|
--| Summary: Get the defined data types of each field instead of SQLite
--|					 storage classes as a comma separated list. _rigPragmaTableInfo
--|          needs to be called beforehand.
--| 
--| Format:  rigDbColumnTypes()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------*/

function rigDbColumnTypes
  if sTableInfo <> empty then
    # EACH LINE OF sTableInfo CONTAINS FIELD META DATA
    repeat for each line tLine in sTableInfo
      # REMOVE INFO ABOUT LENGTH
      if "(" is in tLine then
        put replaceText(tLine, "\(.+\)", "") into tLine
      end if
      # ITEM 3 IS NOT THE STORAGE CLASS, IT IS THE DEFINED DATA TYPE
      put item 3 of tLine into tColumnType
      
      # BUILD TYPES LIST
      put toUpper(tColumnType) & comma after tColumnTypes
    end repeat
   
    delete char -1 of tColumnTypes
   
    return tColumnTypes
  end if
 
  return ""
end rigDbColumnTypes







/*----------------------------------------------------------------------
--| FUNCTION rigDbPrimaryField
--|
--| Author: rabit
--| Version:  1.0
--| Created: 11-04-14
--| Last Mod: --
--| Requires: --
--|
--| Summary: Get the name of the primary field. _rigPragmaTableInfo
--|          needs to be called beforehand.
--| 
--| Format:  rigDbPrimaryField()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------*/

function rigDbPrimaryField
  if sTableInfo <> empty then
    put empty into tPrimaryField
   
    # EACH LINE OF sTableInfo CONTAINS  FIELD META DATA
    repeat for each line tLine in sTableInfo
      if last item of tLine is "1" then
        put item 2 of tLine into tPrimaryField
        exit repeat
      end if
    end repeat
   
    return tPrimaryField
  end if

  return ""
end rigDbPrimaryField






/*----------------------------------------------------------------------
--| FUNCTION rigDbDefaults
--|
--| Author: rabit
--| Version:  1.0
--| Created: 11-04-14
--| Last Mod: --
--| Requires: --
--|
--| Summary: Get the default values of each field as a comma separated list.
--|          _rigPragmaTableInfo needs to be called beforehand.
--| 
--| Format:  rigDbDefaults()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------*/

function rigDbDefaults
  if sTableInfo <> empty then
    # EACH LINE OF sTableInfo CONTAINS FIELD META DATA
    repeat for each line tLine in sTableInfo
      # REMOVE INFO ABOUT LENGTH
      if "(" is in tLine then
        put replaceText(tLine, "\(.+\)", "") into tLine
      end if

      put item -2 of tLine into tDefault
      
      # BUILD DEFAULTS LIST
      put tDefault & comma after tDefaults
    end repeat
   
    delete char -1 of tDefaults
   
    return tDefaults
  end if
  
  return ""
end rigDbDefaults







/*----------------------------------------------------------------------
--| FUNCTION _rigDataSeek
--|
--| Author: rabit
--| Version:  1.0
--| Created: 14-04-14
--| Last Mod: --
--| Requires: --
--|
--| Summary: MOVES THE INTERNAL POINTER TO THE DESIRED OFFSET.  WE CALL
--|                    THIS INTERNALLY BEFORE FETCHING RESULTS TO MAKE SURE THE
--|                    RESULT SET STARTS AT ZERO.
--| 
--| Format:  _rigDataSeek(param1, param2)
--|
--| Parameters: integer <pRecordsetID>, integer <pN> record number
--|
--| Return: bool
----------------------------------------------------------------------*/

function _rigDataSeek pRecordsetID pN
	if pN is empty then
		put 1 into tN
	else
		put pN into tN
	end if

	revMoveToRecord pRecordsetID, tN

	return the result
end _rigDataSeek




/*----------------------------------------------------------------------
--| COMMAND _rigFetchAssoc
--|
--| Author: Trevor DeVore
--| Version:  1.1
--| Created: 17-07-09
--| Last Mod: 02-10-09
--| Requires: --
--|
--| Summary: Returns the result set as an associative array.
--| 
--| Format: _rigFetchAssoc
--|
--| Parameters: integer <pRecordSetID>, reference array <@pRefArray>
--|
--| Return: string
----------------------------------------------------------------------*/

command _rigFetchAssoc pRecordSetID, @pRefArray
	 put revDatabaseColumnNames(pRecordSetID) into tFieldNames

	if tFieldNames begins with "revdberr," then 
		put item 2 to -1 of tFieldNames into tError 
	end if 
    
	if tError is "" then 
		put 0 into tCounter
		repeat until revQueryIsAtEnd(pRecordSetID) 
			add 1 to tCounter
             	
			put 0 into tFieldCounter

			repeat for each item theField in tFieldNames
				add 1 to tFieldCounter
				-- put revDatabaseColumnNamed(pRecordSetID, theField) into pRefArray[tCounter][theField]
				put revDatabaseColumnNamed(pRecordSetID, theField) into pRefArray[tCounter][tFieldCounter] 
				-- put revDatabaseColumnNamed(pRecordSetID, theField) into pRefArray[tCounter][tFieldCounter & "," & theField] 
			end repeat 
             
			revMoveToNextRecord pRecordSetID 
		end repeat 
	end if 
     
	return tError
end _rigFetchAssoc







/*----------------------------------------------------------------------
--| FUNCTION _rigDbLastInsertID
--|
--| Author: Monte Goulding
--| Modification: rabit
--| Version:  1.0
--| Created: 24-02-12
--| Last Mod: 14-04-14
--| Requires: rigDBQuery(), rigDbRow()
--|
--| Summary: Returns the last automatically generated value successfully
--| 				inserted for an AUTO_INCREMENT column as a result of the most
--| 				recently executed INSERT statement.
--| 
--| Format:	_rigDbLastInsertID(param1)
--|
--| Parameters: string <pTable>
--|
--| Return: mixed
----------------------------------------------------------------------*/

function _rigDbLastInsertID pTable
  put rigDBQuery("SELECT last_insert_rowid()") into tQuery
	
	if tQuery["numrows"] = 1 then
		put rigDbRow(1) into tRow
		return tRow["last_insert_rowid()"]
	else
		return FALSE
	end if
end _rigDbLastInsertID







/*----------------------------------------------------------------------
--| FUNCTION rigCurrentConnectionID
--|
--| Author: rabit
--| Version:  1.0
--| Created: 15-04-2014
--| Last Mod: --
--| Requires: rigFetchDBdriverSetting()
--|
--| Summary: Get database connection id.
--| 
--| Format:  rigCurrentConnectionID()
--|
--| Parameters: --
--|
--| Return: integer
----------------------------------------------------------------------*/

function rigCurrentConnectionID
  if gDatabaseID is "" then
    return rigFetchDBdriverSetting("connid")
  end if
  
  return gDatabaseID
end rigCurrentConnectionID






--| END OF sqliteResult.lc
--| Location:  ./system/database/drivers/sqlite/sqliteResult.lc
----------------------------------------------------------------------