<?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
	put "No direct script access allowed."
	exit to top
end if

##
# revIgniter
#
# An application development framework for LiveCode
# modeled on CodeIgniter.
#
# @package		revIgniter
# @author		  rabit@revigniter.com
# @copyright	Copyright (c) 2009 - 2016, dimensionB Bitter u. Bitter GmbH
# @license		http://revigniter.com/userGuide/license.html
# @link		    http://revigniter.com
##

## ------------------------------------------------------------------------

##
# revIgniter HTML Table Generating Library
#
# Lets you create tables manually or from database result arrays or from other arrays.
#
# @package		revIgniter
# @subpackage	Libraries
# @category	  HTML Tables
# @author		  rabit@revigniter.com
# @link		    http://revigniter.com/userGuide/libraries/uri.html
##


# LOGGING
rigLogMessage "debug", "Table Library Loaded"


# DECLARE VARIABLES
local sRows, sHeading, sAutoHeading, sCaption, sTemplate, sNewline, sEmptyCells

put TRUE into sAutoHeading
put return into sNewline





----------------------------------------------------------------------
--| COMMAND rigRunInitialTableConfig
--|
--| Author: rabit
--| Version:  1.0
--| Created: 27-10-09
--| Last Mod: 27-10-09
--| Requires: --
--|
--| Summary: Run initial configuration procedures. Don't remove, even if it does nothing.
--| 
--| Format:  rigRunInitialTableConfig param1
--|
--| Parameters: array <pConfig>
--|
--| Return: empty
----------------------------------------------------------------------

command rigRunInitialTableConfig pConfig

end rigRunInitialTableConfig







----------------------------------------------------------------------
--| COMMAND rigSetTableTemplate
--|
--| Author: rabit
--| Version:  1.0
--| Created: 21-07-09
--| Last Mod: 21-07-09
--| Requires: --
--|
--| Summary: Set the template.
--| 
--| Format:  rigSetTableTemplate param1
--|
--| Parameters: array <pTemplate>
--|
--| Return: empty
----------------------------------------------------------------------

command rigSetTableTemplate pTemplate
	if pTemplate is not an array then
		exit rigSetTableTemplate
	end if

	put pTemplate into sTemplate
end rigSetTableTemplate






----------------------------------------------------------------------
--| COMMAND rigSetTableHeading
--|
--| Author: rabit
--| Version:  1.2
--| Created: 22-07-09
--| Last Mod: 11-04-10
--| Requires: --
--|
--| Summary: Set the table heading.
--|                    Can be passed as an array or a string of comma separated params.
--| 
--| Format:  rigSetTableHeading param1, param2
--|
--| Parameters: mixed <pHeading>, bool <pFirstUpper> uppercase first char of heading words
--|
--| Return: empty
----------------------------------------------------------------------

command rigSetTableHeading pHeading pFirstUpper
	put pHeading into sHeading

	if sHeading is not an array then
		split sHeading using comma
	end if
	
	if pFirstUpper is TRUE then
		repeat for each key tKey in sHeading
			put rigUcwords(sHeading[tKey]) into sHeading[tKey]
		end repeat
	end if
end rigSetTableHeading







----------------------------------------------------------------------
--| FUNCTION rigMakeTableColumns
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-07-09
--| Last Mod: 22-07-09
--| Requires: rigArraySplice()
--|
--| Summary: Set columns.  Takes a one-dimensional array as input and creates
--|                    a multi-dimensional array with a depth equal to the number of
--|                    columns.  This allows a single array with many elements to  be
--|                    displayed in a table that has a fixed column count.
--| 
--| Format:  rigMakeTableColumns(param1, param2, param3)
--|
--| Parameters: array <pArray>, integer <pColLimit>
--|
--| Return: array
----------------------------------------------------------------------

function rigMakeTableColumns pArray pColLimit
	if pArray is not an array then
		return FALSE
	else
	put pArray into tArray
	end if
 
	if (pColLimit is empty) or (pColLimit is not an integer) then
		put 0 into tColLimit
	else
		put pColLimit into tColLimit
	end if

	# TURN OFF THE AUTO-HEADING FEATURE SINCE IT'S DOUBTFUL WE 
	# WILL WANT HEADINGS FROM A ONE-DIMENSIONAL ARRAY
	put FALSE into sAutoHeading

	if tColLimit = 0 then
		return tArray
	end if


	put the number of lines in the keys of tArray into tKeysNum

	put 0 into tCounter  
	put 0 into tOffset

	rigLoadHelper "array"

	repeat while tKeysNum > tOffset
		add 1 to tCounter
             
             put rigArraySplice(tArray, tOffset, tColLimit) into tTempArray
             
		put the number of lines in the keys of tTempArray into tTempKeysNum
             
		# ADD NONE BREAKING SPACES 
		if tTempKeysNum < tColLimit then
			put tTempKeysNum + 1 into tKeyNum
			repeat with i = tTempKeysNum to (tColLimit -1)
				put "&nbsp;" into tTempArray[tKeyNum]
				add 1 to tKeyNum
			end repeat
		end if
             
		put tTempArray into tNewArray[tCounter]

            add tColLimit to tOffset

        end repeat

	return tNewArray
end rigMakeTableColumns







----------------------------------------------------------------------
--| COMMAND rigSetEmptyCellVal
--|
--| Author: rabit
--| Version:  1.1
--| Created: 22-07-09
--| Last Mod: 04-01-10
--| Requires: --
--|
--| Summary: Set "empty" cells. Set a default value for use in any table cells that are empty.
--| 
--| Format:  rigSetEmptyCellVal param1
--|
--| Parameters: string <pValue>
--|
--| Return: empty
----------------------------------------------------------------------

command rigSetEmptyCellVal pValue
/*
	if pValue is not an array then
		put 0 into tCounter
		repeat for each item thisItem in pValue
			add 1 to tCounter
			put thisItem into tEmptyCells[tCounter]
		end repeat
		put tEmptyCells into sEmptyCells
	else
*/
		put pValue into sEmptyCells
	--end if
end rigSetEmptyCellVal






----------------------------------------------------------------------
--| COMMAND rigAddTableRow
--|
--| Author: rabit
--| Version:  1.1
--| Created: 22-07-09
--| Last Mod: 04-11-09
--| Requires: --
--|
--| Summary: Add a table row.
--|                    Can be passed as an array or a string of comma separated params.
--| 
--| Format:  rigAddTableRow param1
--|
--| Parameters: mixed <pRow>
--|
--| Return: empty
----------------------------------------------------------------------

command rigAddTableRow pRow
	put pRow into tRow

	if tRow is not an array then
		split tRow using comma
	end if

	if sRows is an array then
		put (the number of lines in the keys of sRows + 1) into tKeyNum
		put tRow into sRows[tKeyNum]
	else
		put tRow into sRows[1]
	end if
end rigAddTableRow







----------------------------------------------------------------------
--| COMMAND rigSetTableCaption
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-07-09
--| Last Mod: 22-07-09
--| Requires: --
--|
--| Summary: Add a table caption.
--| 
--| Format:  rigSetTableCaption param1
--|
--| Parameters: string <pCaption>
--|
--| Return: empty
----------------------------------------------------------------------

command rigSetTableCaption pCaption
	put pCaption into sCaption
end rigSetTableCaption







/*----------------------------------------------------------------------
--| FUNCTION rigGenerateTable
--|
--| Author: rabit
--| Version:  1.2
--| Created: 22-07-09
--| Last Mod: 04-08-12
--| Requires: _rigSetTableData, rigCompileTableTemplate, rigIsNumberedArray()
--|
--| Summary: Generate the table.
--| 
--| Format:  rigGenerateTable(param1)
--|
--| Parameters: array <pTableData>
--|
--| Return: string
----------------------------------------------------------------------*/

function rigGenerateTable pTableData
	if pTableData is not an array then
		return FALSE
	end if

	if (sHeading is not an array) and (sAutoHeading is FALSE) then
		put FALSE into tSetHeading
	else
		put TRUE into tSetHeading
	end if
	_rigSetTableData pTableData, tSetHeading
	

	# IS THERE ANYTHING TO DISPLAY?  NO?  SMITE THEM!
	if (sHeading is not an array) and (sRows is not an array) then
		return "Undefined table data"
	end if

	# COMPILE AND VALIDATE THE TEMPLATE DATA
	rigCompileTableTemplate
	
	# BUILD THE TABLE!
	put sTemplate["tableOpen"] into tOut
	put tOut & sNewline into tOut

	# ADD ANY CAPTION HERE
	if sCaption is not empty then
		put tOut & sNewline into tOut
		put tOut & "<caption>" & sCaption & "</caption>" into tOut
		put tOut & sNewline into tOut
	end if

	# IS THERE A TABLE HEADING TO DISPLAY?
	if sHeading is an array then
		put tOut & sTemplate["headingRowStart"] into tOut
		put tOut & sNewline into tOut

		# CHECK IF ARRAY IS NUMBERED CONSECUTIVELY
		put TRUE into tHeadingNumberedConsecutive
		put rigIsNumberedArray(sHeading, tHeadingNumberedConsecutive) into tHeadingArrayInfo

		put item 1 of tHeadingArrayInfo into tHeadingNumbered
		
		if tHeadingNumbered is TRUE then
			# USE VALUE OF FIRST LINE OF ARRAY KEYS AS STARTING NUMBER
			put item 2 of tHeadingArrayInfo into tCounter
		else
			put 1 into tCounter
		end if
		
		repeat for each key tKey in sHeading
			if tHeadingNumbered is TRUE then
				put tCounter into tKey
			end if

			put tOut & sTemplate["headingCellStart"] into tOut
			put tOut & sHeading[tKey] into tOut
			put tOut & sTemplate["headingCellEnd"] into tOut
			
			add 1 to tCounter
		end repeat

		put tOut & sTemplate["headingRowEnd"] into tOut
		put tOut & sNewline into tOut
	end if

	# BUILD THE TABLE ROWS
	if sRows is an array then
		# CHECK IF ARRAY IS NUMBERED CONSECUTIVELY
		put TRUE into tConsecutive
		put rigIsNumberedArray(sRows, tConsecutive) into tArrayInfo

		put item 1 of tArrayInfo into tNumbered

		if tNumbered is TRUE then
			# USE VALUE OF FIRST LINE OF ARRAY KEYS AS STARTING NUMBER
			put item 2 of tArrayInfo into tCounter
		else
			put 1 into tCounter
		end if

		repeat for each key tKey in sRows
			if tNumbered is TRUE then
				put tCounter into tKey
			end if

			if sRows[tKey] is not an array then
				exit repeat
			end if

			# WE USE MODULUS TO ALTERNATE THE ROW COLORS
			add 1 to tCounter
			if tCounter mod 2 = 0 then
				put "" into tName
			else
				put "Alt" into tName
			end if

			put "row" & tName & "Start" into tTemplateKey
			put tOut & sTemplate[tTemplateKey] into tOut
			put tOut & sNewline into tOut

			# CHECK IF ARRAY IS NUMBERED CONSECUTIVELY
			put TRUE into tRowNumberedConsecutive
			put rigIsNumberedArray(sRows, tRowNumberedConsecutive) into tRowArrayInfo

			put item 1 of tRowArrayInfo into tRowNumbered

			if tRowNumbered is TRUE then
				# USE VALUE OF FIRST LINE OF ARRAY KEYS AS STARTING NUMBER
				put item 2 of tRowArrayInfo into tRowCounter
			else
				put 1 into tRowCounter
			end if

			repeat for each key tCellKey in sRows[tKey]

				if tRowNumbered is TRUE then
					put tRowCounter into tCellKey
				end if

				put "cell" & tName & "Start" into tTemplateKey
				put tOut & sTemplate[tTemplateKey] into tOut

				if sRows[tKey][tCellKey] is empty then
					put tOut & sEmptyCells into tOut
				else
					put tOut & sRows[tKey][tCellKey] into tOut
				end if

				put "cell" & tName & "End" into tTemplateKey
				put tOut & sTemplate[tTemplateKey] into tOut

				add 1 to tRowCounter

			end repeat
		
			put "row" & tName & "End" into tTemplateKey
			put tOut & sTemplate[tTemplateKey] into tOut
			put tOut & sNewline into tOut
			
			
		end repeat

	end if

	put tOut & sTemplate["tableClose"] into tOut

	return tOut
end rigGenerateTable







----------------------------------------------------------------------
--| COMMAND rigClearTableVars
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-07-09
--| Last Mod: 22-07-09
--| Requires: --
--|
--| Summary: Clears the table arrays.  Useful if multiple tables are being generated.
--| 
--| Format:  rigClearTableVars
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command rigClearTableVars
	put empty into sRows
	put empty into sHeading
	put TRUE into sAutoHeading
end rigClearTableVars







----------------------------------------------------------------------
--| COMMAND _rigSetTableData
--|
--| Author: rabit
--| Version:  1.1
--| Created: 22-07-09
--| Last Mod: 16-12-09
--| Requires: rigIsNumberedArray()
--|
--| Summary: Set table data from an array.
--| 
--| Format:  _rigSetTableData param1, param2
--|
--| Parameters: array <pData>, bool <pSetHeading>
--|
--| Return: empty
----------------------------------------------------------------------

command _rigSetTableData pData, pSetHeading
	if pSetHeading is empty then
		put TRUE into tSetHeading
	else
		put pSetHeading into tSetHeading
	end if

	if pData is not an array then
		return FALSE
	end if

	# CHECK IF ARRAY IS NUMBERED CONSECUTIVELY
	put TRUE into tConsecutive
	put rigIsNumberedArray(sRows, tConsecutive) into tArrayInfo

	put item 1 of tArrayInfo into tNumbered

	put 1 into tCounter
	
	repeat for each key tKey in pData
		if tNumbered is TRUE then
			put tCounter into tKey
		end if

		if pData[tKey] is not an array then
			if sRows is an array then
				put (the number of lines in the keys of sRows) + 1 into tKeyNum
				put pData into sRows[tKeyNum]
			else
				put pData into sRows[1]
			end if
		end if
	
		# IF A HEADING HASN'T ALREADY BEEN SET WE'LL USE THE FIRST ROW OF THE ARRAY AS THE HEADING
		if (tCounter = 1) and (the number of lines in the keys of pData > 1) and (sHeading is not an array) and (tSetHeading is TRUE) then
			put pData[tKey] into sHeading
		else
			if sRows is an array then
				put (the number of lines in the keys of sRows) + 1 into tKeyNum
				put pData[tKey] into sRows[tKeyNum]
			else
				put pData[tKey] into sRows[1]
			end if
		end if

		add 1 to tCounter
		
	end repeat

end _rigSetTableData




----------------------------------------------------------------------
--| COMMAND rigCompileTableTemplate
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-07-09
--| Last Mod: 22-07-09
--| Requires: _rigDefaultTableTemplate()
--|
--| Summary: Compile template.
--| 
--| Format:  rigCompileTableTemplate
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command rigCompileTableTemplate
	if sTemplate is not an array then
		put _rigDefaultTableTemplate() into sTemplate
		exit rigCompileTableTemplate
	end if

	put _rigDefaultTableTemplate() into tTemp

	put "tableOpen,headingRowStart,headingRowEnd,headingCellStart,headingCellEnd,rowStart,rowEnd,cellStart,cellEnd,rowAltStart,rowAltEnd,cellAltStart,cellAltEnd,tableClose" into tKeys
	repeat for each item tKey in tKeys
		if sTemplate[tKey] is empty then
			put tTemp[tKey] into sTemplate[tKey]
		end if
	end repeat
end rigCompileTableTemplate







----------------------------------------------------------------------
--| FUNCTION _rigDefaultTableTemplate
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-07-09
--| Last Mod: 22-07-09
--| Requires: --
--|
--| Summary: Default template.
--| 
--| Format:  _rigDefaultTableTemplate()
--|
--| Parameters: --
--|
--| Return: array
----------------------------------------------------------------------

function _rigDefaultTableTemplate
	put "<table border=" & quote & "0" & quote && "cellpadding=" & quote & "4" & quote && "cellspacing=" & quote & "0" & quote && ">" into tTemplate["tableOpen"]

	put "<tr>" into tTemplate["headingRowStart"]
	put "</tr>" into tTemplate["headingRowEnd"]
	put "<th>" into tTemplate["headingCellStart"]
	put "</th>" into tTemplate["headingCellEnd"]

	put "<tr>" into tTemplate["rowStart"]
	put "</tr>" into tTemplate["rowEnd"]
	put "<td>" into tTemplate["cellStart"]
	put "</td>" into tTemplate["cellEnd"]

	put "<tr>" into tTemplate["rowAltStart"]
	put "</tr>" into tTemplate["rowAltEnd"]
	put "<td>" into tTemplate["cellAltStart"]
	put "</td>" into tTemplate["cellAltEnd"]
	
	put "</table>" into tTemplate["tableClose"]

	return tTemplate
end _rigDefaultTableTemplate







--| END OF Table.lc
--| Location:  ./system/libraries/Table.lc
----------------------------------------------------------------------