<?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
	put "No direct script access allowed."
	exit to top
end if

##
# RevIgniter
#
# A web development framework for Revolution
# modeled on CodeIgniter.
#
# @package		RevIgniter
# @author			rabit@softwaregestaltung.de
# @copyright	Copyright (c) 2009 - 2016, dimensionB Bitter u. Bitter GmbH
# @license		http://revigniter.softwaregestaltung.de/userGuide/license.html
# @link				http://revigniter.softwaregestaltung.de
##

## ------------------------------------------------------------------------

##
# revIgniter User Agent Library
#
#
# @package		revIgniter
# @subpackage	Libraries
# @category		User Agent
# @author			rabit@softwaregestaltung.de
# @link				http://revigniter.softwaregestaltung.de/userGuide/libraries/useragent.html
##

## ------------------------------------------------------------------------


# LOGGING
rigLogMessage "debug", "UserAgent Library Loaded"

# DECLARE VARIABLES
local sAgentAgent, sAgentIsBrowser, sAgentIsRobot, sAgentIsMobile, sAgentLanguages, sAgentCharsets, sAgentPlatforms, sAgentBrowsers, sAgentMobiles
local sAgentRobots, sAgentPlatform, sAgentBrowser, sAgentVersion, sAgentMobile, sAgentRobot, sUseragentsFileLoaded


put FALSE into sAgentIsBrowser
put FALSE into sAgentIsRobot
put FALSE into sAgentIsMobile





----------------------------------------------------------------------
--| COMMAND rigRunInitialUseragentConfig
--|
--| Author: rabit
--| Version:  1.0
--| Created: 27-10-09
--| Last Mod: 27-10-09
--| Requires: --
--|
--| Summary: Run initial configuration procedures. Don't remove, even if it does nothing.
--| 
--| Format:  rigRunInitialUseragentConfig param1
--|
--| Parameters: array <pConfig>
--|
--| Return: empty
----------------------------------------------------------------------

command rigRunInitialUseragentConfig pConfig
	_rigAgentData
end rigRunInitialUseragentConfig







----------------------------------------------------------------------
--| COMMAND _rigAgentData
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: rigTrim(), _rigLoadAgentFile(), _rigCompileData
--|
--| Summary: Sets the user agent and runs the compilation routine.
--| 
--| Format:  _rigAgentData
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command _rigAgentData
	if $_SERVER["HTTP_USER_AGENT"] is not empty then
		put rigTrim($_SERVER["HTTP_USER_AGENT"]) into sAgentAgent
	end if

	if sAgentAgent is not "" then
		if _rigLoadAgentFile() is TRUE then
			_rigCompileData
		end if
	end if
end _rigAgentData





/*----------------------------------------------------------------------
--| FUNCTION _rigLoadAgentFile
--|
--| Author: rabit
--| Version:  1.1
--| Created: 07-10-09
--| Last Mod: 17-09-15
--| Requires: rigLogMessage, rigFetchAgentsData(), rigDeleteAgentsVar
--|
--| Summary: Compile the user agent data.
--| 
--| Format:  _rigLoadAgentFile()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------*/

function _rigLoadAgentFile
	if sUseragentsFileLoaded is not TRUE then
		try
			require gAPPPATH & "config/useragents" & gEXT
		catch err
			rigLogMessage "error", "An error occured while trying to include file:" && "mimes" & gEXT
			return FALSE
				
		end try
		put TRUE into sUseragentsFileLoaded
	end if

	put FALSE into tReturn

	put rigFetchAgentsData("platforms") into sAgentPlatforms
	if sAgentPlatforms is an array then
		rigDeleteAgentsVar "platforms"
		put TRUE into tReturn
	end if

	put rigFetchAgentsData("browsers") into sAgentBrowsers
	if sAgentBrowsers is an array then
		rigDeleteAgentsVar "browsers"
		put TRUE into tReturn
	end if

	put rigFetchAgentsData("mobiles") into sAgentMobiles
	if sAgentMobiles is an array then
		rigDeleteAgentsVar "mobiles"
		put TRUE into tReturn
	end if

	put rigFetchAgentsData("robots") into sAgentRobots
	if sAgentRobots is an array then
		rigDeleteAgentsVar "robots"
		put TRUE into tReturn
	end if
	
	return tReturn
end _rigLoadAgentFile






----------------------------------------------------------------------
--| COMMAND _rigCompileData
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: _rigSetPlatform(), _rigSetBrowser(), _rigSetRobot(), _rigSetMobile()
--|
--| Summary: Compile the user agent data.
--| 
--| Format:  _rigCompileData
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command _rigCompileData
	get _rigSetPlatform()

	if sAgentPlatforms is an array then
		put "_setBrowser,_setRobot,_setMobile" into tFunctions
		
		repeat for each item thisItem in tFunctions
			switch thisItem
        			case "_setBrowser"
             				get _rigSetBrowser()
					if it is TRUE then
						exit _rigCompileData
					end if
					break
				case "_setRobot"
             				get _rigSetRobot()
					if it is TRUE then
						exit _rigCompileData
					end if
					break
				case "_setMobile"
             				get _rigSetMobile()
					if it is TRUE then
						exit _rigCompileData
					end if
					break
    			end switch
		end repeat
	end if
end _rigCompileData






----------------------------------------------------------------------
--| FUNCTION _rigSetPlatform
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Set the platform.
--| 
--| Format:  _rigSetPlatform()
--|
--| Parameters: --
--|
--| Return: mixed
----------------------------------------------------------------------

function _rigSetPlatform
	if sAgentPlatforms is an array then

	repeat with i = 1 to (item 2 of the extents of sAgentPlatforms)
		put line 1 in the keys of sAgentPlatforms[i] into tKey
		put sAgentPlatforms[i][tKey] into tVal

		-- repeat for each key tKey in sAgentPlatforms
			--put sAgentPlatforms[tKey] into tVal

			put rigPregQuote(tKey) into tPregQuotedStr
			
			get matchText(sAgentAgent, "(?i)" & tPregQuotedStr)
			if it is TRUE then
				put tVal into sAgentPlatform
	
				return TRUE
			end if
		end repeat
	end if

	put "Unknown Platform" into sAgentPlatform
end _rigSetPlatform





----------------------------------------------------------------------
--| FUNCTION _rigSetBrowser
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Set the browser.
--| 
--| Format:  _rigSetBrowser()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function _rigSetBrowser
	if sAgentBrowsers is an array then

	repeat with i = 1 to (item 2 of the extents of sAgentBrowsers)
		put line 1 in the keys of sAgentBrowsers[i] into tKey
		put sAgentBrowsers[i][tKey] into tVal

		-- repeat for each key tKey in sAgentBrowsers
			--put sAgentBrowsers[tKey] into tVal

			put rigPregQuote(tKey) into tPregQuotedStr
			
			get matchText(sAgentAgent, "(?i)" & tPregQuotedStr & ".*?([0-9\.]+)", tMatch1)

			if it is TRUE then
				put TRUE into sAgentIsBrowser

				if "Safari" is in sAgentAgent then
                			put offset("Safari/",sAgentAgent) into tSafariOffset
                			put char (tSafariOffset + 7) to (the number of chars in sAgentAgent) of sAgentAgent into sAgentVersion
            			else
                 
                			put tMatch1 into sAgentVersion
            			end if

				put tVal into sAgentBrowser
				get _rigSetMobile()

				return TRUE
			end if
		end repeat
	end if

	return FALSE
end _rigSetBrowser





----------------------------------------------------------------------
--| FUNCTION _rigSetRobot
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Set the robot.
--| 
--| Format:  _rigSetRobot()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function _rigSetRobot
	if sAgentRobots is an array then

	repeat with i = 1 to (item 2 of the extents of sAgentRobots)
		put line 1 in the keys of sAgentRobots[i] into tKey
		put sAgentRobots[i][tKey] into tVal

		--repeat for each key tKey in sAgentRobots
			--put sAgentRobots[tKey] into tVal

			put rigPregQuote(tKey) into tPregQuotedStr
			
			get matchText(sAgentAgent, "(?i)" & tPregQuotedStr)
			if it is TRUE then
				put TRUE into sAgentIsRobot
				put tVal into sAgentRobot

				return TRUE
			end if
		end repeat
	end if

	return FALSE
end _rigSetRobot





----------------------------------------------------------------------
--| FUNCTION _rigSetMobile
--|
--| Author: rabit
--| Version:  1.2
--| Created: 07-10-09
--| Last Mod: 11-05-10
--| Requires: rigPregQuote()
--|
--| Summary: Set the mobile device.
--| 
--| Format:  _rigSetMobile()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function _rigSetMobile
	if sAgentMobiles is an array then
		put toLower(sAgentAgent) into tLowerAgent

		repeat with i = 1 to (item 2 of the extents of sAgentMobiles)
			put line 1 in the keys of sAgentMobiles[i] into tKey
			put sAgentMobiles[i][tKey] into tVal

		--repeat for each key tKey in sAgentMobiles
			--put sAgentMobiles[tKey] into tVal

			put rigPregQuote(tKey) into tPregQuotedStr

			if tKey is in tLowerAgent then

				-- variant
				if (tKey is "os x") or (tKey is "Safari") then
					if ("iphone" is in tLowerAgent) or ("ipod" is in tLowerAgent) or ("ipad" is in tLowerAgent) then
						put TRUE into sAgentIsMobile
						put tVal into sAgentMobile

						return TRUE
					else
						next repeat
					end if
				end if

				put TRUE into sAgentIsMobile
				put tVal into sAgentMobile

				return TRUE
			end if
		end repeat
	end if

	return FALSE
end _rigSetMobile





----------------------------------------------------------------------
--| COMMAND _rigSetLanguages
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: rigTrim(), rigPregReplace()
--|
--| Summary: Set the accepted languages.
--| 
--| Format:  _rigSetLanguages
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command _rigSetLanguages
	put $_SERVER["HTTP_ACCEPT_LANGUAGE"] into tAcceptLang

	if (sAgentLanguages is not an array) and (tAcceptLang is not empty) then
		put rigTrim(tAcceptLang) into tAcceptLang
		put toLower(tAcceptLang) into tAcceptLang
		
		put "(?i)(;q=[0-9\.]+)" into tRegEx
		put rigPregReplace(tAcceptLang, tRegEx, "", "") into sAgentLanguages

		split sAgentLanguages using comma

		if sAgentLanguages is not an array then
			put "Undefined" into sAgentLanguages[1]
		end if
				
	end if
end _rigSetLanguages




----------------------------------------------------------------------
--| COMMAND _rigSetCharsets
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: rigTrim(), rigPregReplace()
--|
--| Summary: Set the accepted character sets.
--| 
--| Format:  _rigSetCharsets
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------

command _rigSetCharsets
	put $_SERVER["HTTP_ACCEPT_CHARSET"] into tAcceptCharset

	if (sAgentLanguages is not an array) and (tAcceptCharset is not empty) then
		put rigTrim(tAcceptCharset) into tAcceptCharset
		put toLower(tAcceptCharset) into tAcceptCharset
		
		put "(?i)(;q=.+)" into tRegEx
		put rigPregReplace(tAcceptCharset, tRegEx, "", "") into sAgentCharsets

		split sAgentCharsets using comma

		if sAgentCharsets is not an array then
			put "Undefined" into sAgentCharsets[1]
		end if
				
	end if
end _rigSetCharsets





----------------------------------------------------------------------
--| FUNCTION rigIsBrowser
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Is browser
--| 
--| Format:  rigIsBrowser()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function rigIsBrowser
	return sAgentIsBrowser
end rigIsBrowser







----------------------------------------------------------------------
--| FUNCTION rigIsRobot
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Is robot
--| 
--| Format:  rigIsRobot()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function rigIsRobot
	return sAgentIsRobot
end rigIsRobot








----------------------------------------------------------------------
--| FUNCTION rigIsMobile
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Is mobile
--| 
--| Format:  rigIsMobile()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function rigIsMobile
	return sAgentIsMobile
end rigIsMobile







----------------------------------------------------------------------
--| FUNCTION rigIsReferral
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Is this a referral from another site?
--| 
--| Format:  rigIsReferral()
--|
--| Parameters: --
--|
--| Return: bool
----------------------------------------------------------------------

function rigIsReferral
	if $_SERVER["HTTP_REFERER"] is not empty then
		return TRUE
	end if

	return FALSE
end rigIsReferral








----------------------------------------------------------------------
--| FUNCTION rigAgentString
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Agent string
--| 
--| Format:  rigAgentString()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigAgentString
	return sAgentAgent
end rigAgentString







----------------------------------------------------------------------
--| FUNCTION rigAgentPlatform
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get platform
--| 
--| Format:  rigAgentPlatform()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigAgentPlatform
	return sAgentPlatform
end rigAgentPlatform







----------------------------------------------------------------------
--| FUNCTION rigBrowser
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get browser name
--| 
--| Format:  rigBrowser()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigBrowser
	if sAgentBrowser is "Mozilla" then
		if "Safari" is in sAgentAgent then
			put "Safari" into sAgentBrowser
		end if
	end if

	return sAgentBrowser
end rigBrowser








----------------------------------------------------------------------
--| FUNCTION rigBrowserVersion
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get browser version
--| 
--| Format:  rigBrowserVersion()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigBrowserVersion
	return sAgentVersion
end rigBrowserVersion








----------------------------------------------------------------------
--| FUNCTION rigRobot
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get robot name
--| 
--| Format:  rigRobot()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigRobot
	return sAgentRobot
end rigRobot








----------------------------------------------------------------------
--| FUNCTION rigMobile
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get the mobile device
--| 
--| Format:  rigMobile()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigMobile
	return sAgentMobile
end rigMobile








----------------------------------------------------------------------
--| FUNCTION rigReferrer
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get the referrer
--| 
--| Format:  rigReferrer()
--|
--| Parameters: --
--|
--| Return: string
----------------------------------------------------------------------

function rigReferrer
	if $_SERVER["HTTP_REFERER"] is not empty then
		put rigTrim($_SERVER["HTTP_REFERER"]) into tReferrer
		return tReferrer
	end if

	return ""
end rigReferrer







----------------------------------------------------------------------
--| FUNCTION rigAgentLanguages
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get the accepted languages
--| 
--| Format:  rigAgentLanguages()
--|
--| Parameters: --
--|
--| Return: array
----------------------------------------------------------------------

function rigAgentLanguages
	if sAgentLanguages is not an array then
		_rigSetLanguages
	end if

	return sAgentLanguages
end rigAgentLanguages







----------------------------------------------------------------------
--| FUNCTION rigAgentCharSets
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Get the accepted charsets
--| 
--| Format:  rigAgentCharSets()
--|
--| Parameters: --
--|
--| Return: array
----------------------------------------------------------------------

function rigAgentCharSets
	if sAgentCharsets is not an array then
		_rigSetCharsets
	end if

	return sAgentCharsets
end rigAgentCharSets








----------------------------------------------------------------------
--| FUNCTION rigAcceptLang
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Test for a particular language.
--| 
--| Format:  rigAcceptLang()
--|
--| Parameters: string <pLang>
--|
--| Return: bool
----------------------------------------------------------------------

function rigAcceptLang pLang
	if pLang is empty then
		put "en" into tLang
	else
		put pLang into tLang
	end if

	put toLower(tLang) into tLang

	if sAgentLanguages is not an array then
		_rigSetLanguages
	end if

	set the itemDelimiter to "-"
	put 0 into tCounter
	repeat for each key tK in sAgentLanguages
		put sAgentLanguages[tK] into tV
		add 1 to tCounter

		# IGNORE ITEM 2 OF LANG LIKE IN "en-US"
		if "-" is in tV then
			put item 1 of tV into tAgentLanguages[tCounter]
		else
			put tV into tAgentLanguages[tCounter]
		end if
	end repeat

	get rigInArray(tAgentLanguages, tLang)

	return it
end rigAcceptLang








----------------------------------------------------------------------
--| FUNCTION rigAcceptCharset
--|
--| Author: rabit
--| Version:  1.0
--| Created: 07-10-09
--| Last Mod: 07-10-09
--| Requires: --
--|
--| Summary: Test for a particular charset.
--| 
--| Format:  rigAcceptCharset()
--|
--| Parameters: string <pCharset>
--|
--| Return: bool
----------------------------------------------------------------------

function rigAcceptCharset pCharset
	if pCharset is empty then
		put "utf-8" into tCharset
	else
		put pCharset into tCharset
	end if

	put toLower(tCharset) into tCharset

	if sAgentCharsets is not an array then
		_rigSetCharsets
	end if

	get rigInArray(sAgentCharsets, tCharset)
		
	return it
end rigAcceptCharset










# CALL INITIAL HANDLERS
--_rigAgentData


--| END OF Useragent.lc
--| Location:  ./system/libraries/Useragent.lc
----------------------------------------------------------------------